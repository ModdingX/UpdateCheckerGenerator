plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group = 'org.moddingx'

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://maven.parchmentmc.org/' }
    maven { url = 'https://maven.moddingx.org/' }
}

dependencies {
    implementation 'org.moddingx:CurseWrapper:2.1'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'com.google.guava:guava:31.1-jre'
}

application.mainClassName = 'org.moddingx.cfupdatechecker.Main'

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

task fatjar(type: Jar) {
    archiveClassifier = "fatjar"
    manifest = jar.manifest
    with jar
}
build.dependsOn fatjar

task configureFatJar {
    doLast {
        configurations.runtimeClasspath.each { dep ->
            fatjar.from(project.zipTree(dep)) {
                exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
    outputs.upToDateWhen { false }
}
fatjar.dependsOn configureFatJar

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url '../moddingx/maven'
        }
    }
}
